group 'cn.attackme'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven{url "http://maven.aliyun.com/nexus/content/groups/public/"}
    mavenCentral()
}

buildscript {
    repositories {
        mavenLocal()
        maven{url "http://maven.aliyun.com/nexus/content/groups/public/"}
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    }
}

dependencies {
    compile 'io.netty:netty-all:4.1.13.Final'
    compile 'com.google.protobuf:protobuf-java:3.2.0'
    compile 'io.grpc:grpc-stub:1.4.0'
    compile 'io.grpc:grpc-protobuf:1.4.0'
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }

    testCompile 'junit:junit:4.12'
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.2.0'
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
        }
    }

    generatedFilesBaseDir = "$projectDir/src"

    generateProtoTasks {
        all()*.plugins{
            grpc{}
        }
    }
}
